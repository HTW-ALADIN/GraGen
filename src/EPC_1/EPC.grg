#using "EPC.gm"

rule initGraph {
    modify {
        x:Event;
        y:Event;
        x -e:ForwardEdge-> y;
    }
}
function randomIntInRange(var lowerBound: int, var upperBound: int) : int {
    return((randomInteger() % (upperBound - lowerBound + 1)) + lowerBound);
}

function randomInteger() : int {
    return((int)Math::round(random() * 10000));
}

function weightMapToRangeMap(ref weightMap:map<string, double>): map<double, string> {
    def ref rangeMap:map<double, string> = map<double, string>{};
    def var sum:double = 0.0;
    for(k:string -> v:double in weightMap) {
        sum = sum + v;
        rangeMap.add(sum, k);
    }
    return(rangeMap);
}

function sampleFromRangeMap(ref rangeMap:map<double,string>): string {
    def var randomValue:double = random();
    def var sampledValue:string;
    def var lowerBound:double = 0.0;

    for(upperBound:double -> v:string in rangeMap) {
        if (randomValue >= lowerBound && randomValue <= upperBound) {
            sampledValue = v;
            break;
        }
    }
    return(sampledValue);
}

function pickReplacementPattern(ref weightedPatterns:map<string, double>): string {
    def ref weightRangePatterns:map<double, string> = weightMapToRangeMap(weightedPatterns);
    def var replacementPattern:string = sampleFromRangeMap(weightRangePatterns);
    return (replacementPattern);
}

procedure generateANDPattern(var edgeAmount:int, x:Node, y:Node) {
    (def a:AndGate) = add(AndGate);
    (def a1:AndGate) = add(AndGate);
    a.Type = "open"; 
    a1.Type = "close";
    add(ForwardEdge, x, a);
    add(ForwardEdge, a1, y);
    for(i:int in [1:edgeAmount]) {
        add(ForwardEdge, a, a1);
    }
    return();
}

procedure generateORPattern(var edgeAmount:int, x:Node, y:Node) {
    (def a:OrGate) = add(OrGate);
    (def a1:OrGate) = add(OrGate);
    a.Type = "open"; 
    a1.Type = "close";
    add(ForwardEdge, x, a);
    add(ForwardEdge, a1, y);
    for(i:int in [1:edgeAmount]) {
        add(ForwardEdge, a, a1);
    }
    return();
}

procedure generateXORPattern(var edgeAmount:int, x:Node, y:Node) {
    (def a:XorGate) = add(XorGate);
    (def a1:XorGate) = add(XorGate);
    a.Type = "open"; 
    a1.Type = "close";
    add(ForwardEdge, x, a);
    add(ForwardEdge, a1, y);
    for(i:int in [1:edgeAmount]) {
        add(ForwardEdge, a, a1);
    }
    return();
}

procedure generateLoopPattern(x:Node, y:Node) {
    (def a:OrGate) = add(OrGate);
    (def a1:XorGate) = add(XorGate);
    a.Type = "open"; 
    a1.Type = "close";
    add(ForwardEdge, x, a);
    add(ForwardEdge, a, a1);
    add(BackwardEdge, a1, a);
    add(ForwardEdge, a1, y);
    return();
}

rule insertGatePattern {
    x:Node -s:ForwardEdge-> y:Node;

    modify {
    ---
        eval {
            def ref weightedPatterns:map<string, double> = map<string, double>{
                "AND"->0.3,
                "OR"->0.3,
                "XOR"->0.2,
                "LOOP"->0.2
            };
            def var replacementPattern:string = pickReplacementPattern(weightedPatterns);
            def var edgeAmount:int = randomIntInRange(2,4);
            switch(replacementPattern) {
                case "AND" {
                    generateANDPattern(edgeAmount, x, y);
                }
                case "OR" {
                    generateORPattern(edgeAmount, x, y);
                }
                case "XOR" {
                    generateXORPattern(edgeAmount, x, y);
                }
                case "LOOP" {
                    generateLoopPattern(x, y);
                }
                else {
                    emit("SHOULD NOT BE HAPPENING", replacementPattern, "\n");
                }
            }
            rem(s);
        }
    }
}

rule alternateEventsAndFunctions {
    alternative {
        EventGateGate {
            e:Event -fe:ForwardEdge-> g:Gate -fe1:ForwardEdge-> g1:Gate;

            modify {
                g -:ForwardEdge-> :Function -:ForwardEdge-> g1;
                delete(fe1);
            }
        }

        FunctionGateGate {
            f:Function -fe:ForwardEdge-> g:Gate -fe1:ForwardEdge-> g1:Gate;

            modify {
                g -:ForwardEdge-> :Event -:ForwardEdge-> g1;
                delete(fe1);
            }
        }

        GateBackwardsGate {
            g:Gate -be:BackwardEdge-> g1:Gate;

            modify {
                g -:BackwardEdge-> :Event -:BackwardEdge-> :Function -:BackwardEdge-> g1;
                delete(be);
            }
        }
    }
}

// TODO remove rule and add check to alternateEventsAndFunctions that excludes the application of EventGateGate when g1 has outgoing backwards edge to n:Node;
rule fixLoop {
    alternative {
        EventGateEvent {
            g:Gate -e:ForwardEdge-> ev:Event -e1:ForwardEdge-> g1:Gate -b:BackwardEdge-> ev1:Event;

            modify {
                g -:ForwardEdge-> g1;
                delete(ev);
            }
        }
    }
}

rule eventAndFunctionPopulation {
    alternative {
        FEvF {
            f:Function -e:Edge-> g:Gate -e1:Edge-> ev:Event -e2:Edge-> g1:Gate -e3:Edge-> f1:Function;
            modify {
                ev -:typeof(e2)-> :Function -:typeof(e2)-> :Event -:typeof(e2)-> g1;
                delete(e2);
            }
        }

        EvFEv {
            ev:Event -e:Edge-> g:Gate -e1:Edge-> f:Function -e2:Edge-> g1:Gate -e3:Edge-> ev1:Event;
            modify {
                f -:typeof(e2)-> :Event -:typeof(e2)-> :Function -:typeof(e2)-> g1;
                delete(e2);
            }
        }
    }
}